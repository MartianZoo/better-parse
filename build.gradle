group 'com.github.h0tk3y.betterParse'
version '0.3.5'

buildscript {
    ext.kotlin_version = '1.2.60'

    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven'

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "junit:junit:4.12"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
        apiVersion = '1.1' // stay compatible with 1.1 stdlib
    }
}

kotlin.experimental.coroutines "enable"

// Code generation

def maxTupleSize = 16

task('codegen').doFirst {
    AndCodegenKt.andCodegen(maxTupleSize, sourceSets.main.kotlin.srcDirs.first().absolutePath + "/generated/andFunctions.kt")
    TupleCodegenKt.tupleCodegen(maxTupleSize, sourceSets.main.kotlin.srcDirs.first().absolutePath + "/generated/tuples.kt")
}

compileKotlin.dependsOn codegen

// Publication

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        publishToBintray(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = findProperty('bintray_user')
    key = findProperty('bintray_api_key')

    publications = ['publishToBintray']
    publish = true
    pkg {
        repo = 'maven'
        name = 'better-parse'
        desc = 'A nice parser combinator library for Kotlin'
        websiteUrl = 'https://github.com/h0tk3y/better-parse'
        issueTrackerUrl = 'https://github.com/h0tk3y/better-parse/issues'
        vcsUrl = 'https://github.com/h0tk3y/better-parse.git'
        licenses = ['Apache-2.0']
        labels = ['kotlin', 'parser', 'parser-combinator', 'dsl']
        publicDownloadNumbers = true

        version {
            name = project.version
            mavenCentralSync {
                sync = false
            }
        }
    }
}